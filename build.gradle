buildscript {
    ext {
        springBootVersion = "2.1.7.RELEASE"
        lombokVersion = "1.18.8"
    }
}

plugins {
    id "java"
    id "java-library"
    id "idea"
    id "org.springframework.boot" version "2.1.7.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.security:spring-security-bom:5.1.6.RELEASE"
        mavenBom "org.springframework.session:spring-session-bom:Bean-SR3"
    }
}

sourceCompatibility = 11
targetCompatibility = 11

compileJava {
    dependsOn clean
    options.encoding = "UTF-8"
    options.compilerArgs += "-Xlint:all"
    options.compilerArgs += "-Xlint:-processing"
    options.compilerArgs += "-Xlint:-classfile"
}

compileTestJava {
    options.compilerArgs += "-Xlint:all"
    options.compilerArgs += "-Xlint:-processing"
    options.compilerArgs += "-Xlint:-classfile"
}

bootJar {
    mainClassName = "yakisuzu.demo.DemoApplication"
    archiveBaseName = project.name
}

task afterBuild {
    doLast {
        def srcJar = "./build/libs/${project.name}.jar"
        def distJar = "./ops/app/app.jar"
        println "mv $srcJar $distJar"
        file(srcJar).renameTo(file(distJar))
    }
}

build {
    finalizedBy afterBuild
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // spring web
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"

    // spring security
    implementation "org.springframework.security:spring-security-web" // spring-security-bom
    implementation "org.springframework.security:spring-security-config" // spring-security-bom

    // spring session
    implementation "org.springframework.session:spring-session-data-redis" // spring-session-bom
    implementation "io.lettuce:lettuce-core:5.1.8.RELEASE"

    // lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // test
    testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "junit", module: "junit" // junit4
        exclude group: "org.hamcrest", module: "hamcrest-library"
        exclude group: "org.hamcrest", module: "hamcrest-core"
    }
    testImplementation "org.springframework.security:spring-security-test"
}

test {
    // https://junit.org/junit5/docs/current/user-guide/#running-tests-ide-intellij-idea
    // https://junit.org/junit5/docs/current/user-guide/#running-tests-build-gradle
    useJUnitPlatform()
}

wrapper {
    gradleVersion = "5.6"
}
